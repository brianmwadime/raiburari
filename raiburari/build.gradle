/*
 * Copyright (c) 2015 Henry Addo
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.noveogroup.android.check'

def globalConfiguration = rootProject.extensions.getByName("ext")

def libVersion = globalConfiguration.getAt("androidVersionName")

// This is the library artifact version
version = libVersion

android {

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName = libVersion
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'  // Some libraries have issues with this
        disable 'OldTargetApi'
        disable 'IconMissingDensityFolder' // Ignore for now
        disable 'UnusedResources' // Ignore for now.
        warningsAsErrors true
        if (System.env.CI == 'true') {
            abortOnError true
            htmlReport false
            xmlReport false
            textReport true
            textOutput 'stdout'
        }
    }

    check {
        abortOnError true
    }
}

dependencies {
    compile project(':raiburari-annotation');
    compile project(':raiburari-processor');
    def appDependencies = rootProject.ext.appDependencies

    apt appDependencies.daggerCompiler
    apt appDependencies.butterKnifeComplier
    apt project(':raiburari-processor');
    compile appDependencies.dagger
    compile appDependencies.design
    compile appDependencies.cardView
    compile appDependencies.recyclerView
    compile appDependencies.butterKnife
    compile appDependencies.rxAndroid
    compile appDependencies.otto

    // Data dependencies
    compile appDependencies.gson
    provided appDependencies.javaxAnnotation
}

ext {
    artifact = 'Raiburari'
    blob = 'The project aims to provide a reusable Android library for building Android apps'
    pkg = 'aar'
}

// Upload artifact to bintray repo
apply from: '../config/bintray-upload.gradle'
